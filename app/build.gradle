plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id "kotlin-kapt"
    id "com.google.dagger.hilt.android"

}

def noKeyFound = '"NO_KEY_FOUND"'
def theMoveDbApiKey = noKeyFound
def sensitiveDataFile = file("../sensitive_data.txt")
Properties sensitiveData = new Properties()

if (sensitiveDataFile.canRead()) {
    // Read the sensitive data from file sensitive_data.txt
    sensitiveData.load(new FileInputStream(sensitiveDataFile))
    if (sensitiveData != null) {
        if (sensitiveData.containsKey('theMoveDbApiKey')) {
            theMoveDbApiKey = sensitiveData['theMoveDbApiKey']
        }
    }
}

android {
    namespace 'com.nfragiskatos.rewind'
    compileSdk 33

    defaultConfig {
        applicationId "com.nfragiskatos.rewind"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "THE_MOVIE_DB_API_KEY", theMoveDbApiKey
        resValue "string", "THE_MOVIE_DB_API_KEY", theMoveDbApiKey

        buildConfigField "String", "THE_MOVIE_DB_API_BASE_URL", "\"https://api.themoviedb.org/3/\""
        buildConfigField "String", "THE_MOVIE_DB_API_IMAGE_BASE_URL", "\"https://image.tmdb.org/t/p/w500\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {

        dataBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"

    // cardview
    implementation "androidx.cardview:cardview:$cardViewVersion"

    // glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"

}

kapt {
    correctErrorTypes = true
}